plugins {
    id 'com.gradle.plugin-publish' version "0.9.7"
    id 'nebula.release' version '6.0.0'
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'java-gradle-plugin'
apply plugin: 'checkstyle'

group = 'de.set.gradle'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

repositories {
    jcenter()
}

dependencies {
    testCompile gradleTestKit()
    testCompile localGroovy()
    testCompile 'com.netflix.nebula:nebula-test:6.1.2'
    testCompile('org.spockframework:spock-core:1.0-groovy-2.4') {
        exclude group: 'org.codehaus.groovy'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.4.1'
}

test {
    testLogging {
        events "failed"
        exceptionFormat "full"
    }
}

gradlePlugin {
    plugins {
        eclipseCompilerPlugin {
            id = 'de.set.ecj'
            implementationClass = 'de.set.gradle.ecj.EclipseCompilerPlugin'
        }
        eclipseCompilerPluginBase {
            id = 'de.set.ecj-base'
            implementationClass = 'de.set.gradle.ecj.EclipseCompilerBasePlugin'
        }
    }
}

checkstyle {
    toolVersion = '8.2'
}

pluginBundle {
    website = 'https://github.com/TwoStone/gradle-eclipse-compiler-plugin'
    vcsUrl = 'https://github.com/TwoStone/gradle-eclipse-compiler-plugin'
    description = 'Gradle plugin for using the eclipse compiler for java (ecj) for compiling java files instead of javac'
    tags = ['ecj', 'eclipse compiler for java', 'java', 'compile']

    plugins {
        ecjPlugin {
            id = 'de.set.ecj'
            displayName = 'Gradle Eclipse Compiler for Java Plugin'
        }
    }
}

tasks.postRelease.dependsOn publishPlugins

